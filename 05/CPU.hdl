// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are comboutMux1tional: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:



    PC(in=outA, load=jump, inc=true, reset=reset, out[0..14]=pc);

    And(a=instruction[15], b=true, out=instruccionC);
    And(a=instruction[12], b=instruccionC, out=memoriaInstruccion);
    Not(in=instruccionC, out=instruccionA);

    And(a=instruction[5], b=instruccionC, out=regA);
    And(a=instruction[4], b=instruccionC, out=regD);
    And(a=instruction[3], b=instruccionC, out=memoria, out=writeM);

    And(a=instruction[2], b=instruccionC, out=instjmplt);
    And(a=instruction[1], b=instruccionC, out=instjmpeq);
    And(a=instruction[0], b=instruccionC, out=instjmpgt);

    DRegister(in=outALU, load=regD, out=outD);

    Or(a=instruccionA, b=regA, out=loadA);
    Mux16(a=instruction, b=outALU, sel=regA, out=outMux1);
    ARegister(in=outMux1, load=loadA, out=outA, out[0..14]=addressM);

    Mux16(a=outA, b=inM, sel=memoriaInstruccion, out=salidAoM);

    ALU(x=outD, y=salidAoM, zx=instruction[11], nx=instruction[10],
           zy=instruction[9], ny=instruction[8], f=instruction[7],
           no=instruction[6], zr=aluCero, ng=aluNeg, out=outM, out=outALU);

    Not(in=aluNeg, out=notaluNeg);
    Not(in=aluCero, out=notaluCero);
    And(a=notaluNeg, b=notaluCero, out=alupv);
    And(a=instjmplt, b=aluNeg, out=jumplt);
    And(a=instjmpeq, b=aluCero, out=jumpeq);
    And(a=instjmpgt, b=alupv, out=jumpgt);
    Or8Way(in[0]=jumplt, in[1]=jumpeq, in[2]=jumpgt, in[3..7]=false, out=jump);

}